%%%%
%%%%  T-PRISM DistMult model for link prediction-- distmult.psm
%%%%
%%%%  Copyright (C) 2019 Ryosuke Kojima
%%%%
%%
%%  This example is to represent a DistMult using T-PRISM.
%%

%%-------------------------------------
%%  Declarations of tensor atoms
%%
tensor_atom(v(_),[20]).
tensor_atom(r(_),[20]).

rel(U,V,E):-tensor(v(U),[i]),tensor(v(V),[i]),tensor(r(E),[i]).


%%------------------------------------
%%  Utility part:
%%
candidate(U,E,Goals,L):-findall(V,(member(rel(_,V,E),Goals),not member(rel(U,V,E),Goals)),L).
random_select_negative(rel(U,V,E),Goals,rel(U,NegativeV,E)):-
	candidate(U,E,Goals,L),
	random_select(L,NegativeV).
generate_preference_pair(Gs,GoalPairList):-
	maplist(G,NegG,random_select_negative(G,Gs,NegG),Gs,NegGs),
	maplist(X,Y,Z,Z=[X,Y],Gs,NegGs,GoalPairList).

%%------------------------------------
%%  Main:
%%
prism_main([]):-
	random_set_seed(1234),
	load_clauses('sample.dat',Gs),
	generate_preference_pair(Gs,GoalPairList),
	GoalPlaceholder=[[rel(_,_,0),rel(_,_,0)]],
	save_placeholder_goals('distmult_sample_tmp/data.json',json,GoalPlaceholder,GoalPairList),
	save_expl_graph('distmult_sample_tmp/expl.json','distmult_sample_tmp/flags.json',GoalPlaceholder).

